robot: gif
5 7 8 
 c O .6.5.  O  ..5.. O o .5.6.o   o5...6
  V  ..5..  |  ..5..  V  ..5.. \./ .526.
[_ ]]77072 [ ] .702.[[_ ]72202[__ ]22202
 ( ) .5.6.  \  ..6..  \  ..6.. \ / .6.5.
  \  ..5.. ( ) .5.6. ( ) .5.6.  \  ..6..
 ( ) .6.5.  \  ..5..  \  ..5.. ( ) .5.6.
  \  ..6.. ( ) .6.5. ( ) .6.5.  \  ..5..

 c O .5.6.  O  ..6.. O o .6.5.o   o6...5
  V  ..6..  |  ..6..  V  ..6.. \./ .675.
[_ ]]22027 [ ] .207.[[_ ]27707[__ ]77707
 ( ) .6.5.  \  ..5..  \  ..5.. \ / .5.6.
  \  ..6.. ( ) .6.5. ( ) .6.5.  \  ..5..
 ( ) .5.6.  \  ..6..  \  ..6.. ( ) .6.5.
  \  ..5.. ( ) .5.6. ( ) .5.6.  \  ..6..
end

dress_white: gif
5 4 2
 ._. .777. ._. .777.
 ) ( .777. ) ( .777.
//..\77777/,.\\77777
/,|\\77777//|.\77777
end

dress_red: gif
5 4 1
 ._. .111.
 | | .111.
//W\\11111
     .....
end

---
adding an empty row to `dress_red` so it's a drop in replacement for `dress_white`.
gifs are spawened with the bottom left corner at (1, 1) 
<o>

right_arm: gif
5 2 8 
 |   .7...  /  ..7..    /....7    /....7
W    2....VV   22...\/\/ 2222.\/\/ 2222.

    /....7  /  ..70. /   .700. |   .7...
\/\/ 2222.VV   22...W    2....W    2....
end

left_arm: gif
5 3 8 
     .....     .....__   77...__   77...
 __  .77.. __  .77..  \__..222  \__..222
   \/...22   \/...22     .....     .....

 |   07...  /  007..     .....     .....
  \__.0222  \__..222  |  ..7..  \  ..7..
     .....     .....   \/...22   \/...22
end

legs: gif
3 1 4
 )\.22
( )2.2
/( 22.
( )2.2
end

ball: gif
1 1 3
O7c7.7
end

ball_cycle: gif
seq dye red ball 
seq take 5 null
seq dye blue ball
seq take 5 null
seq dye yellow ball 
    take 5 null
end

---
I could have also written `ball_cycle` as 

ball_cycle: gif
seq dye red ball seq take 5 null seq dye blue ball seq take 5 null seq dye yellow ball take 5 null
end

It's the same thing
<o>

wheel: gif
join helper 0  2  5  join helper 1  3  5 
join helper 2  4  5  join helper 3  5  6 
join helper 5  7  6  join helper 6  8  6 
join helper 7  9  6  join helper 8  10 5 
join helper 9  11 4  join helper 10 13 5 
join helper 11 13 6  join helper 12 13 7 
join helper 13 13 8  join helper 14 12 9 
join helper 15 11 10 join helper 16 9  11
join helper 17 7  11 join helper 18 5  11
join helper 19 3  10 join helper 20 2  9 
join helper 21 1  8  join helper 22 1  7
     helper 23 1  6 
end

helper: fn int fn int fn int gif
skip $1 move $2 $3 ball_cycle
end

---
Lots of repetition. I made it into a function.
<o>

robot_juggler: fn gif gif
join move 12 1 signiture
join skip 3 move 1 3 left_arm
join skip 5 move 9 3 right_arm
join slow 2 move 5 4 robot
join wheel
join move 5 1 slow 4 $1
     slow 6 move 6 1 legs
end

---
made `robot_juggler` into a function so I can choose the dress that gets displayed

here's how it would look as tree diagram

join 
  move 
    12 
    1 
    signiture
  join 
    skip 
      3 
      move 
        1 
        3 
        left_arm
    join 
      skip 
        5 
        move 
          9 
          3 
          right_arm
      join 
        slow 
          2 
          move 
            5 
            4 
            robot
        join 
          wheel
          join 
            move 
              5 
              1 
              slow 
                4 
                $1
            slow 
              6 
              move 
                6 
                1 
                legs

<o>

main: gif
robot_juggler dye white dress_white
end

---
Try changing the dress to `dress_red` or changing the color
<o>

signiture: gif
2 1 1
OB77
end

