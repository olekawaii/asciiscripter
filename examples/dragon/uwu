-- core types required for the art macro

data int contains
    negative nat
    positive nat
    zero

data nat contains
    one
    succ nat

data coordinate contains
    coordinate int int

data bad_video contains
    empty_video
    cons_frame bad_frame bad_video

data bad_frame contains
    empty_frame
    unsafe_cons_cell cell bad_frame  

data cell contains
    cell coordinate character

data character contains
    space
    char visible_character color

data color contains
    red
    green
    yellow
    blue
    magenta
    cyan
    white
    black

data visible_character contains
    bang
    double_quotes
    pound
    dollar
    percent
    ampersand
    single_quote
    open_paranthesis
    close_paranthesis
    asterisk
    plus
    comma
    hyphen
    period
    slash
    digit_zero
    digit_one
    digit_two
    digit_three
    digit_four
    digit_five
    digit_six
    digit_seven
    digit_eight
    digit_nine
    colon
    semicolon
    less_than
    equals
    greater_than
    question_mark
    at_sign
    uppercase_a
    uppercase_b
    uppercase_c
    uppercase_d
    uppercase_e
    uppercase_f
    uppercase_g
    uppercase_h
    uppercase_i
    uppercase_j
    uppercase_k
    uppercase_l
    uppercase_m
    uppercase_n
    uppercase_o
    uppercase_p
    uppercase_q
    uppercase_r
    uppercase_s
    uppercase_t
    uppercase_u
    uppercase_v
    uppercase_w
    uppercase_x
    uppercase_y
    uppercase_z
    opening_bracket
    backslash
    closing_bracket
    caret
    underscore
    grave_accent
    lowercase_a
    lowercase_b
    lowercase_c
    lowercase_d
    lowercase_e
    lowercase_f
    lowercase_g
    lowercase_h
    lowercase_i
    lowercase_j
    lowercase_k
    lowercase_l
    lowercase_m
    lowercase_n
    lowercase_o
    lowercase_p
    lowercase_q
    lowercase_r
    lowercase_s
    lowercase_t
    lowercase_u
    lowercase_v
    lowercase_w
    lowercase_x
    lowercase_y
    lowercase_z
    opening_brace
    vertical_bar
    closing_brace
    tilde

define dragon of_type video as layer 
    layer 
        by vi shift west by iii shift north center_head
        layer 
            right_side 
            shift east by vi rotate flip_over_y right_side
    by iii shift west by iv shift south swinging_tail

define center_head of_type video as loop art xii ix
   )  (     ...7..7.....    )  (    ....7..7....     )  (   .....7..7...
  ((__))    ..776677....   ((__))   ...776677...    ((__))  ....776677..
  /o..o\    ..636636....   /o..o\   ...636636...    /o..o\  ....636636..
 ( -vv- )   .6|6776|6...  ( -vv- )  ..6|6776|6..   ( -vv- ) ...6|6776|6.
  \ __. \   ..6|666|6...   ) __ (   ...6|66|6...   / .__ /  ...6|666|6..
   )     \  ...6|||||6..  /      \  ..6||||||6..  /     (   ..6|||||6...
  /       \ ..6|||||||6. /        \ .6||||||||6. /       \  .6|||||||6..
 '         ).6|||||||||6(          )6||||||||||6(         ` 6|||||||||6.
          ' ..||||||||6.            .||||||||||. `          .6||||||||..

define swinging_tail of_type video as art vi iv
 \   ).6|||6(   / 6|||6.
  ) / ..6|6. \ (  .6|6..
 ( /  .6|6..  \ ) ..6|6.
  V   ..6...   V  ...6..

define wing of_type video as art xi iv
/\         22.........  /T\.     ..2222.....           ...........
) \        2|2........ / | \`.   .2|2|222...           ...........
 `\\       .222......./ ^|_.\ `. 2.22222|22.|          2..........
    `      ....2......       ``"\.......2222|          2..........
           ...........
|          2..........
|          2..........
           ...........

define right_head of_type video as layer
    by ix rotate by xiv shift east fire
    seq blow roar

define blow of_type video as loop extend art xiv v
.             7..............             7.............
 ),__         .7766......... ),__         .7766.........
  ( o`---~~^, ..6|366666666.  ( o`-.-~~^, ..6|366666666.
 /   -^V^V^V  .6|||6777777.. /     -^V^V  .6|||||67777..
/  `-----.~'  6|.666666666../  `-----.~'  6|.666666666..
.             7..............             7.............
 ),__         .7766......... ),__         .7766.........
  ( o`._,~~^, ..6|366666666.  ( -`.___,~^,..6|3666666666
 /    `  -^V  .6||||6||677.. /    )__   < .6||||666|||6.
/  `---^-.~'  6|.666666666../  `-'   `.~' 6|.666...6666.

define roar of_type video as by ii shift south loop art xiii vii
.            7.............            7.............            7............
 ),__        .7766........ ),__        .7766........ ),__        .7766........
  ( o`---~~^,..6|366666666  ( o`---~~^,..6|366666666  ( o`---~~^,..6|366666666
 /   -^V^V^V .6|||6777777. /    /V'V'V .6||||677777. /    /V'V'V .6||||677777.
/  `-----.~' 6||666666666./  \  \,A,A, 6||6||677777./    (       6||||6.......
  /          ||6..........  / `----.~' ||6.66666666.   \  \,A,A, |||6||677777.
             .............             .............    `----.~' ....66666666.

define right_side of_type video as layer
    layer
        slow ii shift south by iv shift east rotate foot
        layer 
            by vii shift north by vi shift east right_head 
            by ii rotate body
    shift north by ix shift east wing

define fire of_type video as art viii iii
        ........        ........        ........        ........        ........
-       3.......-.      33......-._     133.....-.___   11133... .___)  .11133..
        ........        ........        ........  .     ..3.....   ._   ...13...
    _   ....3...    _   ....3...    _   ....1...    _   ....1...        ........
   __)  ...133..   '_)  ...311..   ( `  ...3.1..   (_   ...13...   .__  ...113..
    ._. ....133.      _.......13       ........1        ........        ........
        ........        ........
     __ .....11.       _.......1
        ........        ........

define foot of_type video as art v ii
\ (  6|6..) /  6|6..
 )_nm.6666|_nm 6666.

define body of_type video as by ii extend art x vii
     /  / .....6||6.     /  / .....6||6.
_..-'  /  66666||6.._..-'  (  66666||6..
      (   ||||||6...        \ ||||||||6.
       \  |||||||6..         )|||||||||6
        ) ||||||||6.        / ||||||||6.
       /  |||||||6..       /  |||||||6..
_.-'  '   6666||6..._.-'  '   6666||6...

define main of_type video as dragon

data video contains
    nil_video
    cons_video frame video

data frame contains
    frame column column horizontal

define fresh_frame of_type frame as
    frame empty_column empty_column empty_horizontal

data horizontal contains
    horizontal grid_cell row row

define empty_horizontal of_type horizontal as
    horizontal empty_grid_cell empty_row empty_row

define insert_horizontal of_type fn int fn character fn horizontal horizontal as
    lambda index dot lambda chr dot lambda horizontal z n p dot
        match index in
            zero            to horizontal full_grid_cell chr n p 
            negative index  to horizontal z insert_row index chr n p
            positive index  to horizontal z n insert_row index chr p

data grid_cell contains
    empty_grid_cell
    full_grid_cell character

data row contains
    empty_row
    cons_row grid_cell row

define insert_row of_type fn nat fn character fn row row as
    lambda index dot lambda chr dot lambda r dot 
        match of_type row match r in
            empty_row     to cons_row empty_grid_cell empty_row
            cons_row _ _  to r
        in
            cons_row head tail to match index in
                one         to cons_row full_grid_cell chr tail
                succ index  to cons_row head insert_row index chr tail
            empty_row to ...  -- unreachable

data column contains
    empty_column
    cons_column horizontal column

define 
    insert_frame 
of_type 
    fn int fn int fn character fn frame frame 
as
    lambda x dot 
    lambda y dot 
    lambda chr dot 
    lambda frame col_neg col_pos row_zero dot
    match y in
        negative index to frame
            insert_column x index chr col_neg
            col_pos
            row_zero
        positive index to frame
            col_neg
            insert_column x index chr col_pos
            row_zero
        zero to frame 
            col_neg
            col_pos
            insert_horizontal x chr row_zero 
        
define insert_column of_type fn int fn nat fn character fn column column as
    lambda x dot lambda y dot lambda chr dot lambda col dot
        match of_type column
            match col in
                empty_column     to cons_column empty_horizontal empty_column
                cons_column _ _  to col
        in 
            cons_column head tail to match y in
                one         to cons_column insert_horizontal x chr head tail
                succ index  to cons_column head insert_column x index chr tail

define from_video of_type fn bad_video video as
    lambda x dot match x in
        empty_video           to nil_video
        cons_frame head tail  to cons_video from_frame head from_video tail

define from_frame of_type fn bad_frame frame as 
    lambda f dot match f in
        unsafe_cons_cell cell coordinate x y chr tail to 
            insert_frame x y chr from_frame tail
        empty_frame to fresh_frame
        
define into_video of_type fn video bad_video as lambda vid dot match vid in
    nil_video             to empty_video
    cons_video head tail  to cons_frame into_frame head into_video tail

define into_frame of_type fn frame bad_frame as 
    lambda frame neg_col pos_col zer_col dot
        uwulayer_frames
            column_to_frame one negative neg_col
            uwulayer_frames
                column_to_frame one positive pos_col    
                horizontal_to_frame zero zer_col 

define horizontal_to_frame of_type fn int fn horizontal bad_frame as 
    lambda y dot lambda horizontal char neg_row pos_row dot
        apply_to_frame
            match char in
                empty_grid_cell   to lambda x dot x
                full_grid_cell x  to unsafe_cons_cell cell coordinate zero y x
            uwulayer_frames 
                row_to_frame one y negative neg_row
                row_to_frame one y positive pos_row

define apply_to_frame of_type 
    fn fn bad_frame bad_frame fn bad_frame bad_frame 
as 
    lambda f dot lambda x dot f x

define row_to_frame of_type fn nat fn int fn fn nat int fn row bad_frame as 
    lambda index dot lambda y dot lambda sign dot lambda r dot match r in
        empty_row           to empty_frame
        cons_row head tail  to 
            apply_to_frame
                match head in
                    empty_grid_cell   to lambda x dot x
                    full_grid_cell x  to 
                        unsafe_cons_cell cell coordinate sign index y x
                row_to_frame succ index y sign tail

define column_to_frame of_type fn nat fn fn nat int fn column bad_frame as 
    lambda index dot lambda sign dot lambda col dot match col in
        empty_column           to empty_frame
        cons_column head tail  to uwulayer_frames 
            horizontal_to_frame sign index head
            column_to_frame succ index sign tail
        
define shift_frame of_type fn direction fn frame frame as
    lambda dir dot lambda f dot match f in
        frame col_neg col_pos hor_zero to match dir in
            north to match col_neg in 
                empty_column to frame
                    empty_column 
                    cons_column hor_zero col_pos
                    empty_horizontal
                cons_column hor col_succ to frame
                    col_succ
                    cons_column hor_zero col_pos
                    hor
            south to match col_pos in 
                empty_column to frame
                    cons_column hor_zero col_neg
                    empty_column 
                    empty_horizontal
                cons_column hor col_succ to frame
                    cons_column hor_zero col_neg
                    col_succ
                    hor
            east to map_columns shift_horizontal right f
            west to map_columns shift_horizontal left f

define map_video of_type fn fn frame frame fn video video as
    lambda f dot lambda x dot match x in 
        nil_video             to nil_video
        cons_video head tail  to cons_video f head map_video f tail

define shift of_type fn direction fn video video as 
    lambda d dot lambda v dot map_video shift_frame d v

data horizontal_direction contains
    right
    left

define shift_horizontal of_type 
    fn horizontal_direction fn horizontal horizontal 
as
    lambda dir dot lambda horizontal z r_l r_r dot
         match dir in
            right to match r_l in 
                cons_row head tail to horizontal head tail cons_row z r_r
                empty_row to horizontal empty_grid_cell empty_row cons_row z r_r
            left to match r_r in
                cons_row head tail to horizontal head cons_row z r_l tail
                empty_row to horizontal empty_grid_cell cons_row z r_l empty_row

define map_columns of_type fn fn horizontal horizontal fn frame frame as
    lambda f dot lambda frame c_d c_u h_z dot
        frame
            map_column f c_d
            map_column f c_u
            f h_z

define map_column of_type fn fn horizontal horizontal fn column column as 
    lambda f dot lambda col dot match col in 
        empty_column           to empty_column
        cons_column head tail  to cons_column f head map_column f tail

define map_video of_type fn fn frame frame fn video video as 
    lambda f dot lambda x dot match x in
        nil_video             to nil_video
        cons_video head tail  to cons_video f head map_video f tail

define layer_frames of_type fn frame fn frame frame as 
    lambda frame na pa za dot lambda frame nb pb zb dot 
        frame 
            layer_columns na nb 
            layer_columns pa pb 
            layer_horizontal za zb

data tuple_col_col contains
    tuple_col_col column column

define layer_columns of_type fn column fn column column as
    lambda ca dot lambda cb dot match tuple_col_col ca cb in
        tuple_col_col empty_column a to a
        tuple_col_col a empty_column to a
        tuple_col_col cons_column head_a tail_a cons_column head_b tail_b to
            cons_column 
                layer_horizontal head_a head_b 
                layer_columns tail_a tail_b

define layer_horizontal of_type fn horizontal fn horizontal horizontal as
    lambda horizontal ca aa ba dot lambda horizontal cb ab bb dot 
        horizontal
            match ca in 
                empty_grid_cell to cb
                _               to ca
            layer_rows aa ab
            layer_rows ba bb

data tuple_row_row contains
    tuple_row_row row row

define layer_rows of_type fn row fn row row as 
    lambda x dot lambda b dot match tuple_row_row x b in
        tuple_row_row empty_row a                    to a
        tuple_row_row a empty_row                    to a
        tuple_row_row cons_row aa ba cons_row ab bb  to
            cons_row
                match aa in 
                    empty_grid_cell to ab
                    _               to aa
                layer_rows ba bb

define video_length of_type fn video whole as 
    lambda x dot match x in
        nil_video          to whole_zero
        cons_video _ tail  to increment_whole video_length tail

define layer of_type fn video fn video video as
    lambda top dot lambda bottom dot match video_length top in
        whole_zero     to bottom
        whole top_len  to match video_length bottom in
            whole_zero        to top
            whole bottom_len  to match lcm top_len bottom_len in
                x to layer_helper 
                     take x loop_video_forever top
                     take x loop_video_forever bottom

define take of_type fn nat fn video video as 
    lambda counter dot lambda bad_frames dot match bad_frames in
        nil_video             to nil_video
        cons_video head tail  to match counter in 
            one     to cons_video head nil_video
            succ a  to cons_video head take a tail

define seq of_type fn video fn video video as
    lambda x dot lambda y dot match x in 
        nil_video to y
        cons_video head tail to cons_video head seq tail y

define loop_video_forever of_type fn video video as 
    lambda x dot seq x loop_video_forever x

define layer_helper of_type fn video fn video video as
    lambda x dot lambda y dot match x in
        nil_video to nil_video
        cons_video head_a tail_a to match y in
            cons_video head_b tail_b to 
                cons_video 
                    layer_frames head_a head_b 
                    layer_helper tail_a tail_b
            nil_video to nil_video

define by of_type fn nat fn fn video video fn video video as
    lambda counter dot lambda fn dot lambda vid dot match counter in
        one     to fn vid
        succ a  to by a fn fn vid

define rotate of_type fn video video as 
    lambda vid dot match vid in
        nil_video           to nil_video
        cons_video head tail  to seq tail cons_video head nil_video

define slow of_type fn nat fn video video as 
    lambda num dot lambda vid dot match vid in
        nil_video             to nil_video
        cons_video head tail  to seq 
            take num loop_video_forever cons_video head nil_video 
            slow num tail

define over_y_hor of_type fn horizontal horizontal as 
    lambda horizontal a b c dot horizontal 
        a 
        map_row flip_over_y_char c 
        map_row flip_over_y_char b

define map_over_grid_cell of_type 
    fn fn character character fn grid_cell grid_cell 
as
    lambda f dot lambda x dot match x in
        empty_grid_cell   to empty_grid_cell
        full_grid_cell a  to full_grid_cell f a

define map_horizontal of_type 
    fn fn character character fn horizontal horizontal 
as 
    lambda f dot lambda horizontal c ra rb dot horizontal
        map_over_grid_cell f c
        map_row f ra
        map_row f rb

define map_row of_type fn fn character character fn row row as 
    lambda f dot lambda x dot match x in
        empty_row           to empty_row
        cons_row head tail  to cons_row
            map_over_grid_cell f head
            map_row f tail

define flip_over_y of_type fn video video as map_video map_columns over_y_hor

define reverse of_type fn video video as 
    lambda x dot match x in
        nil_video       to nil_video
        cons_video a b  to seq reverse b cons_video a nil_video

define tail of_type fn video video as lambda x dot match x in
    nil_video       to nil_video
    cons_video _ b  to b

define loop of_type fn video video as lambda x dot seq x tail reverse tail x

define extend of_type fn video video as lambda x dot match x in 
    nil_video                  to nil_video
    cons_video head nil_video  to cons_video head cons_video head nil_video
    cons_video head tail       to cons_video head extend tail

data direction contains
    north
    south
    east
    west

define flip_over_y_char of_type fn character character as 
    lambda x dot match x in
        space         to space
        char c color  to char
            match c in
                single_quote       to grave_accent
                open_paranthesis   to close_paranthesis
                close_paranthesis  to open_paranthesis
                comma              to period
                slash              to backslash
                less_than          to greater_than
                greater_than       to less_than
                opening_bracket    to closing_bracket
                backslash          to slash
                closing_bracket    to opening_bracket
                underscore         to underscore
                grave_accent       to single_quote
                opening_brace      to closing_brace
                closing_brace      to opening_brace
                _                  to c
            color

define sum_coordinate of_type fn coordinate fn coordinate coordinate as
    lambda coordinate x_i y_i dot lambda coordinate x_ii y_ii dot
        coordinate sum_int x_i x_ii sum_int y_i y_ii

define eq_int of_type fn int fn int bool as
    lambda x dot lambda y dot match tuple_int_int x y in
       tuple_int_int zero       zero        to true
       tuple_int_int positive a positive b  to eq_nat a b
       tuple_int_int negative a negative b  to eq_nat a b
       _                                    to false

data tuple_int_int contains
   tuple_int_int int int

define sum_int of_type fn int fn int int as 
    lambda x dot lambda y dot match x in 
        zero        to y
        negative _  to sum_int increment x decrement y 
        positive _  to sum_int decrement x increment y 

define increment of_type fn int int as lambda x dot match x in 
   zero             to positive one
   positive a       to positive succ a
   negative one     to zero 
   negative succ a  to negative a

define decrement of_type fn int int as lambda x dot match x in
   zero             to negative one
   negative a       to negative succ a
   positive one     to zero
   positive succ a  to positive a

define recolor_character of_type fn color fn character character as
    lambda color dot lambda character dot match character in
        space     to space
        char a _  to char a color

data bool contains
    true
    false

define and of_type fn bool fn bool bool as
    lambda x dot match x in
        false to const_bool_bool false
        true  to id_bool

define or of_type fn bool fn bool bool as
    lambda x dot match x in
        true  to const_bool_bool true
        false to id_bool

define id_bool of_type fn bool bool as lambda x dot x

define const_bool_bool of_type fn bool fn bool bool as 
    lambda x dot lambda _ dot x

define eq_nat of_type fn nat fn nat bool as
    lambda x dot lambda y dot match tuple_nat_nat x y in
        tuple_nat_nat one    one     to true
        tuple_nat_nat succ a succ b  to eq_nat a b
        _                            to false

data tuple_nat_nat contains
   tuple_nat_nat nat nat

define eq_coordinate of_type fn coordinate fn coordinate bool as
    lambda coordinate a b dot lambda coordinate c d dot and eq_int a c eq_int b d

define insert of_type fn cell fn bad_frame bad_frame as
    lambda c dot lambda bad_frame dot match c in
        cell coord _ to match bad_frame in
            empty_frame                 to unsafe_cons_cell c empty_frame
            unsafe_cons_cell head tail  to match head in
                cell coord_ii _ to match eq_coordinate coord_ii coord in
                    true  to unsafe_cons_cell c tail
                    false to unsafe_cons_cell head insert c tail

data option_nat contains
    some_nat nat
    none_nat

data option_whole contains
    some_whole whole
    none_whole

data whole contains
    whole_zero
    whole nat

define subtract_exactly of_type fn whole fn whole option_whole as
    lambda x dot lambda y dot match x in
        whole a to match y in
            whole_zero  to some_whole whole a
            whole b     to subtract_exactly
                match a in
                    one     to whole_zero
                    succ n  to whole n
                match b in
                    one     to whole_zero
                    succ n  to whole n
        whole_zero to match y in
            whole_zero  to some_whole whole_zero
            whole _     to none_whole

define divide_exactly of_type fn whole fn whole option_whole as
    lambda x dot match x in
        whole_zero  to lambda _ dot some_whole whole_zero
        whole _     to lambda y dot match subtract_exactly x y in
            none_whole    to none_whole
            some_whole a  to map_option_whole
                lambda x dot match x in
                    whole_zero  to whole one
                    whole a     to whole succ a
                divide_exactly a y

define map_option_whole of_type fn fn whole whole fn option_whole option_whole as
    lambda f dot lambda x dot match x in
        none_whole    to none_whole
        some_whole a  to some_whole f a

define sum_nat of_type fn nat fn nat nat as
    lambda x dot match x in
        one     to succ
        succ a  to lambda y dot succ sum_nat a y

define product_nat of_type fn nat fn nat nat as 
    lambda x dot lambda y dot match x in
        one     to y
        succ a  to sum_nat y product_nat a y

define product_int of_type fn int fn int int as 
    lambda x dot lambda y dot match tuple_int_int x y in
        tuple_int_int zero       _           to zero
        tuple_int_int _          zero        to zero
        tuple_int_int positive a positive b  to positive product_nat a b
        tuple_int_int positive a negative b  to negative product_nat a b
        tuple_int_int negative a positive b  to negative product_nat a b
        tuple_int_int negative a negative b  to positive product_nat a b
        
data list_whole contains
    cons_whole whole list_whole
    nil_whole

data option_list_whole contains
    some_list_whole list_whole
    none_list_whole

define map_option_list_whole of_type 
    fn 
        fn list_whole list_whole 
        fn option_list_whole option_list_whole 
as
    lambda f dot lambda x dot match x in
        some_list_whole a  to some_list_whole f a
        none_list_whole    to none_list_whole

define greater of_type fn nat fn nat bool as 
    lambda a dot lambda b dot match a in
        one     to false
        succ c  to match b in
            one     to true
            succ d  to greater c d

define divisible of_type fn whole fn whole bool as
    lambda x dot lambda y dot match divide_exactly x y in
        some_whole _  to true
        none_whole    to false

define gcd of_type fn nat fn nat nat as
    lambda x dot lambda y dot gcd_helper i ii x y

define gcd_helper of_type fn nat fn nat fn nat fn nat nat as 
    lambda output dot lambda try dot lambda x dot lambda y dot
    match or greater try x greater try y in
        false to 
            gcd_helper
                match
                    and 
                        divisible whole x whole try 
                        divisible whole y whole try 
                    in
                        true  to try
                        false to output
                succ try
                x
                y
        true to output

define lcm of_type fn nat fn nat nat as
    lambda x dot lambda y dot
        match divide_exactly whole product_nat x y whole gcd x y in
            none_whole    to i
            some_whole x  to match x in
              whole x     to x
              whole_zero  to i

define prime_factors of_type fn nat fn whole list_whole as
    lambda x dot lambda y dot match y in
        whole aaa to match aaa in
            one     to nil_whole 
            succ _  to match divide_exactly y whole x in
                some_whole a  to cons_whole whole x prime_factors x a
                none_whole    to prime_factors succ x y
        whole_zero to nil_whole 

define uwulayer_frames of_type fn bad_frame fn bad_frame bad_frame as
    lambda top dot lambda bottom dot match top in
        unsafe_cons_cell head tail  to insert head uwulayer_frames tail bottom
        empty_frame                 to bottom

define increment_whole of_type fn whole whole as lambda x dot match x in
    whole_zero  to whole one
    whole x     to whole succ x

data nat_wrapper contains
    wrapped nat

define i of_type nat as one

define ii of_type nat as succ i

define iii of_type nat as succ ii

define iv of_type nat as succ iii

define v of_type nat as succ iv

define vi of_type nat as succ v

define vii of_type nat as succ vi

define viii of_type nat as succ vii

define ix of_type nat as succ viii

define x of_type nat as succ ix

define xi of_type nat as succ x

define xii of_type nat as succ xi

define xiii of_type nat as succ xii

define xiv of_type nat as succ xiii

define xv of_type nat as succ xiv

define xvi of_type nat as succ xv

define xvii of_type nat as succ xvi

define xviii of_type nat as succ xvii

define xix of_type nat as succ xviii

define xx of_type nat as succ xix

define xxi of_type nat as succ xx

define xxii of_type nat as succ xxi

define xxiii of_type nat as succ xxii

define xxiv of_type nat as succ xxiii

define xxv of_type nat as succ xxiv

define xxvi of_type nat as succ xxv

define xxvii of_type nat as succ xxvi

define xxviii of_type nat as succ xxvii

define xxix of_type nat as succ xxviii

define xxx of_type nat as succ xxix

define xxxi of_type nat as succ xxx

define xxxii of_type nat as succ xxxi

define xxxiii of_type nat as succ xxxii

define xxxiv of_type nat as succ xxxiii

define xxxv of_type nat as succ xxxiv

define xxxvi of_type nat as succ xxxv

define xxxvii of_type nat as succ xxxvi

define xxxviii of_type nat as succ xxxvii

define xxxix of_type nat as succ xxxviii

define xl of_type nat as succ xxxix

define xli of_type nat as succ xl

define xlii of_type nat as succ xli

define xliii of_type nat as succ xlii

define xliv of_type nat as succ xliii

define xlv of_type nat as succ xliv

define xlvi of_type nat as succ xlv

define xlvii of_type nat as succ xlvi

define xlviii of_type nat as succ xlvii

define xlix of_type nat as succ xlviii

define l of_type nat as succ xlix

define li of_type nat as succ l

define lii of_type nat as succ li

define liii of_type nat as succ lii

define liv of_type nat as succ liii

define lv of_type nat as succ liv

define lvi of_type nat as succ lv

define lvii of_type nat as succ lvi

define lviii of_type nat as succ lvii

define lix of_type nat as succ lviii

define lx of_type nat as succ lix

define lxi of_type nat as succ lx

define lxii of_type nat as succ lxi

define lxiii of_type nat as succ lxii

define lxiv of_type nat as succ lxiii

define lxv of_type nat as succ lxiv

define lxvi of_type nat as succ lxv

define lxvii of_type nat as succ lxvi

define lxviii of_type nat as succ lxvii

define lxix of_type nat as succ lxviii

define lxx of_type nat as succ lxix

define lxxi of_type nat as succ lxx

define lxxii of_type nat as succ lxxi

define lxxiii of_type nat as succ lxxii

define lxxiv of_type nat as succ lxxiii

define lxxv of_type nat as succ lxxiv

define lxxvi of_type nat as succ lxxv

define lxxvii of_type nat as succ lxxvi

define lxxviii of_type nat as succ lxxvii

define lxxix of_type nat as succ lxxviii

define lxxx of_type nat as succ lxxix

define lxxxi of_type nat as succ lxxx

define lxxxii of_type nat as succ lxxxi

define lxxxiii of_type nat as succ lxxxii

define lxxxiv of_type nat as succ lxxxiii

define lxxxv of_type nat as succ lxxxiv

define lxxxvi of_type nat as succ lxxxv

define lxxxvii of_type nat as succ lxxxvi

define lxxxviii of_type nat as succ lxxxvii

define lxxxix of_type nat as succ lxxxviii

define xc of_type nat as succ lxxxix

define xci of_type nat as succ xc

define xcii of_type nat as succ xci

define xciii of_type nat as succ xcii

define xciv of_type nat as succ xciii

define xcv of_type nat as succ xciv

define xcvi of_type nat as succ xcv

define xcvii of_type nat as succ xcvi

define xcviii of_type nat as succ xcvii

define xcix of_type nat as succ xcviii

define c of_type nat as succ xcix
